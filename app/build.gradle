apply plugin: 'com.android.application'
apply from: rootProject.file('gradle-tools/server.gradle')
apply from: rootProject.file('gradle-tools/apikey.gradle')
apply from: rootProject.file('gradle-tools/version.gradle')
apply from: rootProject.file('gradle-tools/keystore.gradle')

/*==============================================================================
 Copyright (c) 2017 Rizky Kharisma (@ngengs)


 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0
 
 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
 =============================================================================*/

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId 'com.ngengs.skripsi.todongban'
        minSdkVersion 16
        targetSdkVersion 27
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [
                mapsAPI: rootProject.ext.apiKeyGoogleMaps
        ]
        buildConfigField "String", "SERVER_ADDRESS", rootProject.ext.apiServerAddress
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.name != 'debug') {
                outputFileName = "${applicationId}-v${versionName}-build.${getBuildDate()}-${variant.name}.apk"
            } else {
                outputFileName = "${applicationId}-${variant.name}.apk"
            }
        }
    }

    signingConfigs {
        release {
            if (rootProject.ext.keyExist) {
                storeFile file(rootProject.ext.keyStoreLocationRelease)
                storePassword rootProject.ext.keyStorePasswordRelease
                keyAlias rootProject.ext.keyAliasRelease
                keyPassword rootProject.ext.keyPasswordRelease
            }
        }

        debug {
            if (rootProject.ext.keyExist) {
                storeFile file(rootProject.ext.keyStoreLocationDebug)
                storePassword rootProject.ext.keyStorePasswordDebug
                keyAlias rootProject.ext.keyAliasDebug
                keyPassword rootProject.ext.keyPasswordDebug
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            if (rootProject.ext.keyExist) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree(include: ['*.pro'], dir: 'proguards').asList().toArray()
        }

        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
            debuggable true
            if (rootProject.ext.keyExist) {
                signingConfig signingConfigs.debug
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'InvalidPackage' // For retrofit
    }
}

static def getBuildDate() {
    def date = new Date().format('yyyyMMdd')
    return date
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    androidTestApi('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:animated-vector-drawable:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:support-v13:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation 'com.google.android.gms:play-services-identity:11.6.2'
    implementation 'com.google.android.gms:play-services-location:11.6.2'
    implementation 'com.google.android.gms:play-services-places:11.6.2'
    implementation 'com.google.android.gms:play-services-maps:11.6.2'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0', {
        exclude group: 'com.android.support'
    }
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'co.infinum:materialdatetimepicker-support:3.3.1'
    implementation 'com.google.firebase:firebase-messaging:11.6.2'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    testApi 'junit:junit:4.12'
    annotationProcessor 'com.android.support:support-annotations:27.0.2'
}

apply plugin: 'com.google.gms.google-services'